plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

static def generateVersionCode() {
    def retval = "git rev-list HEAD --count".execute().text.trim() ?: "0"
    return retval.toInteger()
}

ext {
    _groupId = "com.kosoku"
    _artifactId = "kirby"
    _versionCode = generateVersionCode()
    _versionName = "0.0.1-alpha04"

    _libraryName = "Kirby"
    _libraryDescription = "A UI library for native Android mobile development"
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 24
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    api 'androidx.core:core-ktx:1.7.0'
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'com.google.android.material:material:1.5.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.3'

    //dependency injection
    api "com.google.dagger:hilt-android:2.41"
    kapt "com.google.dagger:hilt-android-compiler:2.41"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    //Rx
    api 'io.reactivex.rxjava3:rxkotlin:3.0.1'
    api 'io.reactivex.rxjava3:rxandroid:3.0.0'

    //logging
    api 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId _groupId
                artifactId _artifactId
                version _versionName

                from components.release

                artifact androidSourcesJar

                pom {
                    name = _libraryName
                    description = _libraryDescription
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Kosoku/Kirby")
                credentials {
                    username = System.getenv("KOSOKU_GPR_USER")
                    password = System.getenv("KOSOKU_GPR_TOKEN")
                }
            }
        }
    }
}

publish.dependsOn assemble